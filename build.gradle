plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    // id 'checkstyle'

    // enables automatic version numbers from git tags
    // and provides additional gradle tasks to create releases (optional)
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id 'maven-publish'
}
group 'de.saar.minecraft'
description 'minecraft-nlg'

// This defines releases as revisions
// that have a tag of the form "release-X.Y.Z"
scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'release'
    }
}
version = scmVersion.version

sourceCompatibility = 1.11

repositories {
    mavenLocal()
    mavenCentral()
    maven {url 'http://www.ling.uni-potsdam.de/tcl/maven2/external'}
    maven {url 'http://www.ling.uni-potsdam.de/tcl/maven2/releases'}
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.1')
    compile (group: "com.github.coli-saar", name:"alto", version:"edc8e69ae3d887f69d918ebb8aa76c41976509d7")
    compile 'info.picocli:picocli:3.9.3'
    implementation 'com.google.code.gson:gson:2.8.6'
    compile 'org.eclipse.collections:eclipse-collections-api:9.2.0'
    compile 'org.eclipse.collections:eclipse-collections:9.2.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// checkstyle {
//     toolVersion "8.19"
// }

mainClassName = 'de.saar.coli.minecraft.Main'


// Define two additional tasks that build a sources jar and a javadoc
// jar.  Both are published at the maven repository.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
	failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// define the github package registry as the maven repository we push our software to.
// this happens automatically thanks to the github actions settings.
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/minecraft-saar/minecraft-nlg")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
