interpretation string: de.up.ling.irtg.algebra.StringAlgebra
interpretation ref: de.up.ling.irtg.algebra.SetAlgebra
interpretation sem: de.up.ling.irtg.algebra.SubsetAlgebra

// a referent is always the location the current subtree refers to
// Terminalsymbole dÃ¼rfen nur in einer einzigen Regel auftreten

// Refers to the wall if all indefPPs also refer to the indefinite
// wall and nothing else.
// (size_1(?1)) -> all PPs refer to the same thing
// intersect_1 -> this thing is the indefwall


// NPs
////////////////////////////////////////////////////////////////////////
IndefNP! -> np(N) [0.9]
  [string] *("a", ?1)
  [ref] ?1
  [sem] ?1

DefNP -> dnp(N)  [0.9]
  [string] *("the", ?1)
  [ref] size_1(?1)
  [sem] ?1


// objects
////////////////////////////////////////////////////////////////////////
N -> obj(Obj) [0.9]
  [string] ?1
  [ref] ?1
  [sem] ?1


Obj -> bridge [0.9]
  [string] "bridge"
  [ref] bridge
  [sem] type

Obj -> wall [0.9]
  [string] "wall"
  [ref] wall
  [sem] type

Obj -> row [0.9]
  [string] "row"
  [ref] row
  [sem] type

Obj -> railing [0.9]
  [string] "railing"
  [ref] railing
  [sem] type

Obj -> block [0.9]
  [string] "block"
  [ref] block
  [sem] type

Obj -> floor [0.9]
  [string] "floor"
  [ref] floor
  [sem] type


// locations
////////////////////////////////////////////////////////////////////////
LocP -> top(DefNP) [0.9]
  [string] *("on top of", ?1)
  [ref] project_1(intersect_2(top-of,?1))
  [sem] x1+y1+z1+x2+y2+z2

LocP -> below(DefNP) [0.9]
  [string] *("below of", ?1)
  [ref] project_2(intersect_1(top-of,?1))
  [sem] x1+y1+z1+x2+y2+z2
  
LocP -> left(DefNP) [0.9]
  [string] *("to the left of", ?1)
  [ref] project_1(intersect_2(left-of,?1))
  [sem] x1+y1+z1+x2+y2+z2
  
LocP -> right(DefNP) [0.9]
  [string] *("to the right of", ?1)
  [ref] project_2(intersect_1(left-of,?1))
  [sem] x1+y1+z1+x2+y2+z2
  
LocP -> front(DefNP) [0.9]
  [string] *("in front of", ?1)
  [ref] project_1(intersect_2(in-front-of,?1))
  [sem] x1+y1+z1+x2+y2+z2
  
LocP -> behind(DefNP) [0.9]
  [string] *("behind", ?1)
  [ref] project_2(intersect_1(in-front-of,?1))
  [sem] x1+y1+z1+x2+y2+z2


// relations
////////////////////////////////////////////////////////////////////////

N -> from(N, DefNP) [0.9]
  [string] *(?1, *("from", ?2))
  [ref] intersect(project_1(intersect_2(from, ?2)), ?1)
  [sem] union(?1, x1+y1+z1)

N -> fromaway(N, DefNP) [0.9]
  [string] *(?1, *("from", ?2))
  [ref] intersect(project_1(intersect_2(fromaway, ?2)), ?1)
  [sem] union(?1, x1+y1+z2)

N -> fromtopof(N, DefNP) [0.9]
  [string] *(?1, *("from the top of", ?2))
  [ref] intersect(project_1(intersect_2(fromtopof, ?2)), ?1)
  [sem] union(?1, x1+y1+z1)

N -> to(N, DefNP) [0.9]
  [string] *(?1, *("to", ?2))
  [ref] intersect(project_1(intersect_2(to,?2)), ?1)
  [sem] union(?1, x2+y2+z2)

N -> totopof(N, DefNP) [0.9]
  [string] *(?1, *("to the top of", ?2))
  [ref] intersect(project_1(intersect_2(totopof,?2)), ?1)
  [sem] union(?1, x2+y2+z2)

N -> tobottom(N, DefNP) [0.9]
  [string] *(?1, *("to", ?2))
  [ref] intersect(project_1(intersect_2(tobottom,?2)), ?1)
  [sem] union(?1, x2+y1+z2)

N -> tohere(N, DefNP) [0.9]
  [string] *(?1, *("to", ?2))
  [ref] intersect(project_1(intersect_2(tohere,?2)), ?1)
  [sem] union(?1, x2+y2+z1)


// on the other side of the bridge
N -> othersideof(N, DefNP) [0.9]
  [string] *(?1, *("on the other side of", ?2))
  [ref] intersect(?1, project_1(intersect_3(intersect_2(otherside, ?2), it)))
  [sem] union(?1, x1+y1+z1+x2+y2+z2)

N -> otherside(N) [0.3]
  [string] *(?1, "on the other side")
  [ref] intersect(?1, size_1(project_1(intersect_3(otherside, it))))
  [sem] union(?1, x1+y1+z1+x2+y2+z2)

// Use a location as additional description
N -> loc(N,LocP) [0.9]
  [string] *(?1, ?2)
  [ref] intersect(?1,?2)
  [sem] union(?1, ?2)

N -> height2(N) [0.9]
  [string] *(?1, "of height two")
  [ref] intersect(height2, ?1)
  [sem] union(height, ?1)

N -> height3(N) [0.9]
  [string] *(?1, "of height three")
  [ref] intersect(height3, ?1)
  [sem] union(height, ?1)

N -> height4(N) [0.9]
  [string] *(?1, "of height four")
  [ref] intersect(height4, ?1)
  [sem] union(height, ?1)

N -> length2(N) [0.9]
  [string] *(?1, "of length two")
  [ref] intersect(length2, ?1)
  [sem] union(length, ?1)

N -> length3(N) [0.9]
  [string] *(?1, "of length three")
  [ref] intersect(length3, ?1)
  [sem] union(length, ?1)

N -> length4(N) [0.9]
  [string] *(?1, "of length four")
  [ref] intersect(length4, ?1)
  [sem] union(length, ?1)

N -> toright(N) [0.9]
  [string] *(?1, "to the right")
  [ref] intersect(orientleftright, ?1)
  [sem] union(orientation, ?1)

N -> orientaway(N) [0.9]
  [string] *(?1, "away from you")
  [ref] intersect(orientaway, ?1)
  [sem] union(orientation, ?1)

// adjectives
////////////////////////////////////////////////////////////////////////
Obj -> orange(Obj) [0.9]
  [string] *("orange", ?1)
  [ref] intersect(orange, ?1)
  [sem] dunion(color, ?1)

Obj -> yellow(Obj) [0.9]
  [string] *("yellow", ?1)
  [ref] intersect(yellow, ?1)
  [sem] dunion(color, ?1)

Obj -> blue(Obj) [0.9]
  [string] *("blue", ?1)
  [ref] intersect(blue, ?1)
  [sem] dunion(color, ?1)

Obj -> green(Obj) [0.9]
  [string] *("green", ?1)
  [ref] intersect(green, ?1)
  [sem] dunion(color, ?1)

Obj -> red(Obj) [0.9]
  [string] *("red", ?1)
  [ref] intersect(red, ?1)
  [sem] dunion(color, ?1)

Obj -> redw(Obj) [0.9]
  [string] *("red", ?1)
  [ref] intersect(red_wool, ?1)
  [sem] dunion(color, ?1)

Obj -> bluew(Obj) [0.9]
  [string] *("blue", ?1)
  [ref] intersect(blue_wool, ?1)
  [sem] dunion(color, ?1)

Obj -> yelloww(Obj) [0.9]
  [string] *("yellow", ?1)
  [ref] intersect(yellow_wool, ?1)
  [sem] dunion(color, ?1)

Obj -> blackw(Obj) [0.9]
  [string] *("black", ?1)
  [ref] intersect(black_wool, ?1)
  [sem] dunion(color, ?1)

Obj -> orangew(Obj) [0.9]
  [string] *("orange", ?1)
  [ref] intersect(orange_wool, ?1)
  [sem] dunion(color, ?1)


// TODO: what is the sem interpretation for this??
// probably not important because this rule can only be used
// to describe definite objects and the sem does not propagate to
// indefinite descriptions
Obj -> prev(Obj) [0.9]
  [string] *("previous", ?1)
  [ref] intersect(it, ?1)
  [sem] dunion(x1+y1+z1+color, ?1)

/*
IndefPP -> indefHeight(IndefPP) [0.9]
  [string] *("of height three", ?1)
  [ref] intersect(heightthree, ?1)
  [sem] union(height, ?1)

IndefPP -> widthThree(IndefPP) [0.9]
  [string] *("of length three", ?1)
  [ref] intersect(widththree, ?1)
  [sem] union(width, ?1)

IndefPP -> depthThree(IndefPP) [0.9]
  [string] *("of length three", ?1)
  [ref] intersect(depththree, ?1)
  [sem] union(depth, ?1)


IndefPP -> indefSameShapeIt(IndefPP) [0.9]
  [string] *("of the same shape as the previous one", ?1)
  [ref] intersect(project_1(intersect_2(sameshape, it)), ?1)
  [sem] union(shape, ?1)
*/
